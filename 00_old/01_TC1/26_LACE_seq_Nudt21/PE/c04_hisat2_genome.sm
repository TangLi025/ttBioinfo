SAMPLE=["P0_input","P0_IP_1","P0_IP_2","P0_IP_3","P9_input","P9_IP_1","P9_IP_2","P9_IP_3"]

READ=["1","2"]

METHOD=["raw","dedup"]

INDEX="/disk/user_08/Data/reference/index/Hisat2/mm39_plus_ERCC_index/mm39_plus_ERCC"

MAPPING_THREADS = 10

# 根据样本命名修改sample、rep等
rule all:
  input:
    expand("03_hisat2_mapping/{method}/01_bam_raw/{sample}.bam.bai",method=METHOD,sample=SAMPLE),
    expand("info/03_hisat2_mapping/{method}/01_bam_raw/flagstat/flagstat_summary.txt",method=METHOD)
    #expand("info/03_hisat2_mapping/{method}/01_bam_raw/flagstat/flagstat_summary_num.txt",method=METHOD)


# 注意链特异性类型 
# --rna-strandness RF （R2为forward，有义链）
# 如果比对率不高，可以尝试 --very-sensitive
rule hisat2_mapping_raw:
  input:
    "02_fastq_derRNA/{method}/00_derRNA_fastq/{sample}_1.fq.gz",
    "02_fastq_derRNA/{method}/00_derRNA_fastq/{sample}_2.fq.gz"
  output:
    bam="03_hisat2_mapping/{method}/01_bam_raw/{sample}.bam",
    summary="info/03_hisat2_mapping/{method}/01_bam_raw/{sample}.summary.txt",
    R1="03_hisat2_mapping/{method}/00_unMap/{sample}_1.fq.gz",
    R2="03_hisat2_mapping/{method}/00_unMap/{sample}_2.fq.gz"
  params:
    index=INDEX,
    outname="03_hisat2_mapping/{method}/00_unMap/{sample}_%.fq.gz"
  log:
    "logs/03_hisat2_mapping/{method}/01_bam_raw/{sample}.log"
  threads: MAPPING_THREADS
  shell:
    """
    /disk/user_09/anaconda3/envs/LinLong/bin/hisat2 -x {params.index} \
    --un-conc-gz {params.outname} \
    --rna-strandness RF \
    --summary-file {output.summary} \
    -p {threads} -1 {input[0]} -2 {input[1]} \
    | /disk/user_09/anaconda3/envs/LinLong/bin/samtools view -@ {threads} -bS \
    | /disk/user_09/anaconda3/envs/LinLong/bin/samtools sort -@ {threads} -o {output.bam} \
    1> {log} 2>&1
    """

rule bam_index_raw:
  input:
    "03_hisat2_mapping/{method}/01_bam_raw/{sample}.bam"
  output:
    "03_hisat2_mapping/{method}/01_bam_raw/{sample}.bam.bai"
  log:
    "logs/03_hisat2_mapping/{method}/01_bam_raw_index/{sample}.log"
  threads: 2
  shell:
    "/disk/user_09/anaconda3/envs/LinLong/bin/samtools index -@ {threads} {input} 1> {log} 2>&1"
    
rule bam_flagstat_raw:
  input:
    "03_hisat2_mapping/{method}/01_bam_raw/{sample}.bam"
  output:
    "info/03_hisat2_mapping/{method}/01_bam_raw/flagstat/{sample}.flagstat"
  log:
    "logs/03_hisat2_mapping/{method}/01_bam_raw/flagstat/{sample}.log"
  threads: 2
  shell:
    "/disk/user_09/anaconda3/envs/LinLong/bin/samtools flagstat -@ {threads} {input} 1> {output} 2> {log}"

rule bam_flagstat_summary_raw:
  input:
    expand("info/03_hisat2_mapping/{method}/01_bam_raw/flagstat/{sample}.flagstat",method="{method}",sample=SAMPLE)
  output:
    "info/03_hisat2_mapping/{method}/01_bam_raw/flagstat/flagstat_summary_num.txt",
    "info/03_hisat2_mapping/{method}/01_bam_raw/flagstat/flagstat_summary.txt"
  log:
    "logs/03_hisat2_mapping/{method}/01_bam_raw/flagstat/flagstat_summary.log"
  threads: 2
  shell:
    """
    grep 'properly' {input} | cut -d ":" -f 2 | cut -d "+" -f 1  > {output[0]}
    grep 'properly' {input} > {output[1]}
    """
